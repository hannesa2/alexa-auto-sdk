// For generateAbiFiltersDeploy()
apply from: "${project.rootDir}/abifilter.gradle"

buildscript {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def architecture = 'x86-64'
def packageName = "com.amazon.aace"
def version = '2.2.0'
def libraryVersion = version + '-' + architecture

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 23
        versionCode 1
        versionName '3.2'
        ndk {
            abiFilters = generateAbiFiltersDeploy()
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared', "-DANDROID_PLATFORM_DIR=${project.rootDir}/.."
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable true
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java']
            }
        }
    }
    dependencies {
        api project(":core")
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

// Because the components are created only during the afterEvaluate phase, you must
// configure your publications using the afterEvaluate() lifecycle method.
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = packageName
                artifactId = project.getName()
                version = libraryVersion
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir("https://${betalabs_nexus_url}/repository/maven-releases")
                credentials {
                    username = "${betalabs_nexus_user}"
                    password = "${betalabs_nexus_pw}"
                }
            }
        }
    }
}
